
group 'io.sellmair'
version '2.0.0-alpha.7'

buildscript {

    ext.kotlin_version = "1.3.20"
    ext.coroutines_version = "1.1.1"

    repositories {
        jcenter()
        maven { url 'https://plugins.gradle.org/m2/' }
        maven { url 'https://dl.bintray.com/jetbrains/kotlin-native-dependencies' }
        maven { url "http://dl.bintray.com/kotlin/kotlin-eap" }
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlin_version}"
    }
}


apply plugin: 'kotlin-multiplatform'

allprojects {
    repositories {
        jcenter()
        mavenCentral()
        maven { url "http://dl.bintray.com/kotlin/kotlin-eap" }
    }
}

apply plugin: 'maven-publish'




// TODO work around for https://youtrack.jetbrains.com/issue/KT-27170
configurations {
    compileClasspath
}

kotlin {
    sourceSets {
        commonMain {
            dependencies {
                implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core-common:${coroutines_version}"
                implementation "org.jetbrains.kotlin:kotlin-stdlib-common"
            }
        }
        commonTest {
            dependencies {
                implementation 'org.jetbrains.kotlin:kotlin-test-common'
                implementation 'org.jetbrains.kotlin:kotlin-test-annotations-common'
            }
        }

        jvmMain {
            dependencies {
                implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:${coroutines_version}"
                implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
            }
        }

        jvmTest {
            dependencies {
                implementation 'org.jetbrains.kotlin:kotlin-test'
                implementation 'org.jetbrains.kotlin:kotlin-test-junit'
            }
        }

        macosMain {
            dependencies {
                implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core-native:${coroutines_version}"
            }
        }

        macosTest {
            dependsOn commonTest
        }

        iosArm64Main {
            dependsOn macosMain
        }

        iosArm64Test {
            dependsOn macosTest
        }

        iosX64Main {
            dependsOn macosMain
        }

        iosX64Test {
            dependsOn macosTest
        }

    }


    targets {
        fromPreset(presets.jvm, 'jvm')
        fromPreset(presets.macosX64, 'macos')
        fromPreset(presets.iosArm64, 'iosArm64')
        fromPreset(presets.iosX64, 'iosX64')

        all {
            compilations.all {
                tasks[compileKotlinTaskName].kotlinOptions {
                    freeCompilerArgs = ['-Xuse-experimental=kotlin.Experimental']
                }
            }
        }
    }
}






